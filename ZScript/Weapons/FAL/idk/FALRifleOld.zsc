// ------------------------------------------------------------
// 7.76mm LMG
// ------------------------------------------------------------
const HDLD_FNFAL="fal";

class HD_FNFAL:HDWeapon{
	default{
		//$Category "Weapons/Hideous Destructor"
		//$Title "Chives Herstal MG77 ''Defender''"
		//$Sprite "FALPA0"

		+hdweapon.fitsinbackpack
		obituary "%o was hit with the right hand of the free world from %k.";
		weapon.selectionorder 41;
		weapon.slotnumber 6;
		weapon.kickback 30;
		weapon.bobrangex 0.3;
		weapon.bobrangey 0.9;
		//weapon.bobspeed 2.5;
		scale 0.65;
		inventory.pickupmessage "You got the 7.76 Support Weapon!";
		hdweapon.barrelsize 35,1,2;
		//hdweapon.nicename "Chives Herstal MG77 ''Defender''";
		tag "UN-AF L7A1 ''Defender''";
		hdweapon.refid HDLD_FNFAL;
		hdweapon.loadoutcodes "
			\cubolt - 0/1, heavier bolt for a slower rate of fire";
	}
	override bool AddSpareWeapon(actor newowner){return AddSpareWeaponRegular(newowner);}
	override hdweapon GetSpareWeapon(actor newowner,bool reverse){return GetSpareWeaponRegular(newowner,reverse);}
	override double gunmass(){
		return 13+weaponstatus[FALG_MAG]*0.1;
	}
	override double weaponbulk(){
		int mg=weaponstatus[FALG_MAG];
		int mgt=weaponstatus[FALG_MAGTYPE];
		if(mg<0)return 132;
		else return (132+ENC_776DRUM_LOADED)+mg*ENC_776_LOADED;
		
		//the foregrip and bipod makes it a little bulkier than a non-bp liberator
	}
	//override void failedpickupunload(){
	//	failedpickupunloadmag(FALG_MAG,"HD_FALMag");
	//}
	override void DropOneAmmo(int amt){
		if(owner){
			amt=clamp(amt,1,10);
			if(owner.countinv("SevenMilAmmo"))owner.A_DropInventory("SevenMilAmmo",amt*75);
			else {owner.A_DropInventory("HD_FALMag",amt);}//owner.A_DropInventory("HD_FALBoxMag",amt);
		}
	}
	override string,double getpickupsprite(){
	return "FALP"..((weaponstatus[FALG_MAG]<0)?"B":"A").."0";
	}
		override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		if(sb.hudlevel == 1){
			int nextmagloaded=sb.GetNextLoadMag(hdmagammo(hpl.findinventory("HD_FALMag")));
			if(nextmagloaded>=75){
				sb.drawimage("FALMNORM",(-54,-3),sb.DI_SCREEN_CENTER_BOTTOM);
			}else if(nextmagloaded<1){
				sb.drawimage("FALMEMPTY",(-54,-3),sb.DI_SCREEN_CENTER_BOTTOM,alpha:nextmagloaded?0.6:1.);
			}else sb.drawbar(
				"FALMNORM","FALMGREY",
				nextmagloaded,75,
				(-54,-3),-1,
				sb.SHADER_VERT,sb.DI_SCREEN_CENTER_BOTTOM
			);
			
			sb.drawnum(hpl.countinv("HD_FALMag"),-53,-8,sb.DI_SCREEN_CENTER_BOTTOM);
		}
		
		sb.drawwepcounter(hdw.weaponstatus[FALG_AUTO],
			-20,-12,"RBRSA3A7","STFULAUT"
		);
		
		if(hdw.weaponstatus[FALG_MAGTYPE]>0){sb.drawwepnum(hdw.weaponstatus[FALG_MAG],35);}
		else{sb.drawwepnum(hdw.weaponstatus[FALG_MAG],75);}

		if(hdw.weaponstatus[FALG_CHAMBER]==2){
			sb.drawrect(-19,-11,3,1);
		}
		
	}
	
	override string gethelptext(){
		return
		WEPHELP_FIRESHOOT
		..WEPHELP_RELOAD.."  Reload mag\n"
		//..WEPHELP_USE.."+"..WEPHELP_RELOAD.."  Reload chamber\n"
		..WEPHELP_FIREMODE.."  Semi/Auto\n"
		..WEPHELP_MAGMANAGER
		..WEPHELP_UNLOADUNLOAD
		;
	}
	
	override void DrawSightPicture(
		HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl,
		bool sightbob,vector2 bob,double fov,bool scopeview,actor hpc,string whichdot
	){
		int cx,cy,cw,ch;
		[cx,cy,cw,ch]=screen.GetClipRect();
		sb.SetClipRect(
			-16+bob.x,-4+bob.y,32,16,
			sb.DI_SCREEN_CENTER
		);
		vector2 bobb=bob*3;
		bobb.y=clamp(bobb.y,-8,8);
		sb.drawimage(
			"FALS_001",(0,-1)+bobb,sb.DI_SCREEN_CENTER|sb.DI_ITEM_TOP,
			alpha:0.9,scale:(1.0,1.0)
		);
		sb.SetClipRect(cx,cy,cw,ch);
		sb.drawimage(
			"FALS_000",(0,-8.75)+bob,sb.DI_SCREEN_CENTER|sb.DI_ITEM_TOP
		);
	}
	states{
	select0:
		FALG A 0 A_Overlay(2,"droop");
		FALG A 0 A_JumpIf(invoker.weaponstatus[FALG_MAG]>-1,2);
		F4LG A 0;
		#### # 0;
		goto select0bfg;
	deselect0:
		FALG A 0 A_JumpIf(invoker.weaponstatus[FALG_MAG]>-1,2);
		F4LG A 0;
		#### # 0;
		goto deselect0big;
		
	droop:
		TNT1 A 1{
			if(pitch<frandom(5,8)&&(!gunbraced())&& invoker.weaponstatus[FALG_MAGTYPE]<1){
				if(countinv("IsMoving")>2 && invoker.weaponstatus[FALG_MAG]>-1 && countinv("PowerStrength")<1){    
					A_MuzzleClimb(frandom(-0.075,0.075),
						frandom(0.1,clamp(1-pitch,0.05,0.6)));
				}else if (invoker.weaponstatus[FALG_MAG]>-1){
					A_MuzzleClimb(frandom(-0.045,0.045),
						frandom(0.1,clamp(1-pitch,0.03,0.4)));
				}
			}
		}loop;
		
	ready:
		FALG A 0 A_JumpIf(((invoker.weaponstatus[FALG_MAG]>-1)&&(invoker.weaponstatus[FALG_MAGTYPE]==0)),2);
		F4LG A 0;
		#### # 1{
			A_SetCrosshair(21);
			A_WeaponReady(WRF_ALL);
		}
		goto readyend;
	user3:
		---- A 0 A_MagManager("HD_FALMag");
		goto ready;
	altfire:
	althold:
		goto nope;
	hold:
		#### A 0{
			if(
				//full auto
				invoker.weaponstatus[FALG_AUTO]==2
			)setweaponstate("fire2");
			else if(
				//burst
				invoker.weaponstatus[FALG_AUTO]<1
			)setweaponstate("nope");
		}goto fire;
	user2:
	firemode:
		#### A 1			{
			int aut=invoker.weaponstatus[FALG_AUTO];
			if(aut>=0){
				invoker.weaponstatus[FALG_AUTO]=aut==0?1:0;
			}
		}goto nope;
		
	fire:
		#### A 2;
	fire2:
		F4LG A 0 A_JumpIf(invoker.weaponstatus[FALG_MAG]<0,2);
		FALG A 0;
		#### A 1 offset(0,33){
			if(invoker.weaponstatus[FALG_CHAMBER]==2){
				A_GunFlash();
			}else{
				if(invoker.weaponstatus[FALG_MAG]>0)setweaponstate("chamber_manual");
				else setweaponstate("nope");
			}
		}
		#### B 1 offset(0,36){
				
				if(invoker.weaponstatus[FALG_CHAMBER]==1){
				A_EjectCasing("HDSpent7mm",
					frandom(-1,2),
					(frandom(0.5,0.7),-frandom(15,15.5),frandom(0,0.2)),
					(0,0,-2)
				);}
				invoker.weaponstatus[FALG_CHAMBER]=0;
		}
		#### A 0 offset(0,34) {if(invoker.weaponstatus[0]&FALF_HEAVYBOLT)A_SetTics(2);}
		#### A 1 offset(0,33){
			A_StartSound("weapons/libchamber",CHAN_6,volume:0.6);
			if(invoker.weaponstatus[FALG_MAG]>0){
				invoker.weaponstatus[FALG_MAG]--;
				invoker.weaponstatus[FALG_CHAMBER]=2;}
		}
		#### A 0 offset(0,32){
			if(invoker.weaponstatus[FALG_AUTO]==1)A_ReFire("fire2");
			A_WeaponReady(WRF_NOFIRE);
		}
		goto nope;
		
	flash:
		FALF A 0{
			HDBulletActor.FireBullet(self,"HDB_776");

			A_AlertMonsters();
			A_StartSound("weapons/FALshot",CHAN_WEAPON,0, 1.5);
			invoker.weaponstatus[FALG_CHAMBER]=1;
		}
		FALF # 0{
			HDFlashAlpha(random(32,64));
			A_Light1();
			A_ZoomRecoil(0.97);
				A_MuzzleClimb(
					0,0,
					-0.07,-0.14,
					-frandom(0.3,0.6),-frandom(1.,1.4),
					-frandom(0.2,0.4),-frandom(1.,1.4)
				);
		}
		FALF # 0 A_Jump(128,3);
		FALF AC 1 Bright HDFlashAlpha(random(32,64));
		goto lightdone;
		FALF BD 1 Bright HDFlashAlpha(random(32,64));
		goto lightdone;


	unloadchamber:
		#### # 1 offset(-1,34);
		#### # 2 offset(-2,36);
		#### # 2 offset(-3,38) 
		{
			A_StartSound("weapons/libchamber",CHAN_6,volume:0.6);
			{
			class<actor>which=invoker.weaponstatus[FALG_CHAMBER]>1?"HDLoose7mm":"HDSpent7mm";
			invoker.weaponstatus[FALG_CHAMBER]=0;
			A_EjectCasing(which,
					frandom(-1,2),
					(frandom(0.2,0.3),-frandom(7,7.5),frandom(0,0.2)),
					(0,0,-2)
				);
			}
		}
		#### # 4 offset(-5,37);
		#### # 2 offset(-2,36);
		#### # 1 offset(0,34);
		#### ### 2 offset(0,32);
		goto readyend;
		
	loadchamber:
		---- A 0 A_JumpIf(invoker.weaponstatus[FALG_CHAMBER]>0,"nope");
		---- A 0 A_JumpIf(!countinv("SevenMilAmmo"),"nope");
		---- A 1;
		---- A 1 offset(0,34) A_StartSound("weapons/pocket",CHAN_5);
		#### A 2 offset(2,36);
		#### B 3 offset(3,38);
		#### B 7 offset(5,40);
		#### B 7 offset(5,39);
		#### B 6 offset(4,39){
			if(countinv("SevenMilAmmo")){
				A_TakeInventory("SevenMilAmmo",1,TIF_NOTAKEINFINITE);
				invoker.weaponstatus[FALG_CHAMBER]=2;
				A_StartSound("weapons/rifleclick2",CHAN_7);
			}
		}
		#### B 5 offset(5,37);
		#### A 2 offset(2,35) A_StartSound("weapons/libchamber",CHAN_6,volume:0.6);
		#### A 1 offset(0,33);
		goto readyend;
		
	chamber_manual:
		#### A 1 offset(-1,34){
			if(
				invoker.weaponstatus[FALG_CHAMBER]==2
				||invoker.weaponstatus[FALG_MAG]<1
			)setweaponstate("nope");
		}
		#### B 1 offset(-2,36){
			A_StartSound("weapons/libchamber",8,CHANF_OVERLAP);
			invoker.weaponstatus[FALG_MAG]--;
			invoker.weaponstatus[FALG_CHAMBER]=2;
		}
		#### B 2 offset(-2,38);
		#### B 2 offset(-1,34);
		#### A 1 offset(0,33);
		goto nope;
		
	user4:
	unload:
		FALG A 0 A_JumpIf(invoker.weaponstatus[FALG_MAG]>-1,2);
		F4LG A 0;
		#### # 0{
			invoker.weaponstatus[0]|=FALF_JUSTUNLOAD;
			if(
				invoker.weaponstatus[FALG_MAG]>=0
			)setweaponstate("unmag");
			else if(invoker.weaponstatus[FALG_CHAMBER]>0)setweaponstate("unloadchamber");
		}goto nope;
	reload:
		#### # 0{
			invoker.weaponstatus[0]&=~FALF_JUSTUNLOAD;
			if((invoker.weaponstatus[FALG_MAG]>=75)&&(invoker.weaponstatus[FALG_MAGTYPE]==0))setweaponstate("nope");
			else if((invoker.weaponstatus[FALG_MAG]>=35)&&(invoker.weaponstatus[FALG_MAGTYPE]==1))setweaponstate("nope");
			else if(HDMagAmmo.NothingLoaded(self,"HD_FALMag")){
				if(
					invoker.weaponstatus[FALG_MAG]<0
					&&countinv("SevenMilAmmo")
				)setweaponstate("loadchamber");
				else setweaponstate("nope");
			}
		}goto unmag;
	unmag:
		F4LG A 0 A_JumpIf(invoker.weaponstatus[FALG_MAG]<0,"nomag_unmag");
		//F4LG A 0 A_JumpIf(invoker.weaponstatus[FALG_MAGTYPE]>0,"unmag_boxmag");
		FALG C 2 A_SetCrosshair(21);
		FALG D 3;
		FALG E 4;
		FALG F 4 A_StartSound("weapons/FALdrumout",CHAN_WEAPON);
		FALG G 5{
			A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
			A_StartSound("weapons/smgmagmove",CHAN_WEAPON);
		}
		FALG H 4 A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
		FALG I 3;
		FALG # 0{
			int magamt=invoker.weaponstatus[FALG_MAG];
			if(magamt<0){
				setweaponstate("magout");
				return;
			}
			invoker.weaponstatus[FALG_MAG]=-1;
			if(
				(!PressingUnload()&&!PressingReload())
				||A_JumpIfInventory("HD_FALMag",0,"null")
			){
				HDMagAmmo.SpawnMag(self,"HD_FALMag",magamt);
				setweaponstate("magout");
			}else{
				HDMagAmmo.GiveMag(self,"HD_FALMag",magamt);
				A_StartSound("weapons/pocket",CHAN_WEAPON);
				setweaponstate("pocketmag");
			}
		}
		goto pocketmag;
		
	/*unmag_boxmag:
		F4LG A 0 A_JumpIf(invoker.weaponstatus[FALG_MAG]<0,"nomag_unmag");
		FAL2 C 1 A_SetCrosshair(21);
		FAL2 D 2;
		FAL2 E 3;
		FAL2 F 4 A_StartSound("weapons/FALmagout",CHAN_WEAPON);
		FAL2 G 2{
			A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
			A_StartSound("weapons/smgmagmove",CHAN_WEAPON);
		}
		FAL2 H 2 A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
		FAL2 I 2;
		FAL2 # 0{
			int magamt=invoker.weaponstatus[FALG_MAG];
			if(magamt<0){
				setweaponstate("magout");
				return;
			}
			invoker.weaponstatus[FALG_MAG]=-1;
			if(
				(!PressingUnload()&&!PressingReload())
				||A_JumpIfInventory("HD_FALBoxMag",0,"null")
			){
				HDMagAmmo.SpawnMag(self,"HD_FALBoxMag",magamt);
				setweaponstate("magout");
			}else{
				HDMagAmmo.GiveMag(self,"HD_FALBoxMag",magamt);
				A_StartSound("weapons/pocket",CHAN_WEAPON);
				setweaponstate("pocketmag");
			}
		}
		goto pocketmag;*/
	nomag_unmag:
		F5LG A 3 A_SetCrosshair(21);
		F5LG BCD 3 A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
		F5LG FGHI 4;
		F5LG # 0{
			int magamt=invoker.weaponstatus[FALG_MAG];
			if(magamt<0){
				setweaponstate("magout");
				return;
			}
			invoker.weaponstatus[FALG_MAG]=-1;
			if(
				(!PressingUnload()&&!PressingReload())
				||A_JumpIfInventory("HD_FALMag",0,"null")
			){
				HDMagAmmo.SpawnMag(self,"HD_FALMag",magamt);
				setweaponstate("magout");
			}else{
				HDMagAmmo.GiveMag(self,"HD_FALMag",magamt);
				A_StartSound("weapons/pocket",CHAN_WEAPON);
				setweaponstate("pocketmag");
			}
		}
	pocketmag:
		FALG I 3;
		#### # 0 A_JumpIf(invoker.weaponstatus[FALG_MAG]>-1,2);
		#### # 0;
		#### I 7 offset(2,34) A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
		#### I 7 offset(6,32) A_StartSound("weapons/pocket",CHAN_WEAPON);
		#### I 7 offset(5,34) A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
		#### I 7 offset(4,34) A_StartSound("weapons/pocket",CHAN_WEAPON);
	magout:
		#### I 0 A_JumpIf(invoker.weaponstatus[FALG_MAG]>-1,2);
		#### I 0;
		#### I 0{
			if(invoker.weaponstatus[0]&FALF_JUSTUNLOAD)setweaponstate("reloadend");
			else setweaponstate("loadmag");
		}

	loadmag:
		#### # 0 A_JumpIf(invoker.weaponstatus[FALG_MAGTYPE]>0,"loadmag_box");
		FALG I 26 offset(2,34) A_StartSound("weapons/pocket",CHAN_WEAPON);
		FALG I 7 offset(4,34) A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
		FALG J 7 offset(3,38) A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
		FALG K 7 offset(2,34) A_StartSound("weapons/smgmagmove",CHAN_WEAPON);
		FALG L 6 offset(3,38);
		FALG M 3 offset(0,34);
		FALG N 3{
			let mmm=hdmagammo(findinventory("HD_FALMag"));
			if(mmm){
				invoker.weaponstatus[FALG_MAG]=mmm.TakeMag(true);
				A_StartSound("weapons/FALdrumin",CHAN_BODY);
			}
		}
		FALG O 3;
		#### # 0 A_JumpIf(invoker.weaponstatus[FALG_CHAMBER]<=1,"chamber");
		#### # 0{
			if(
				invoker.weaponstatus[FALG_MAG]<1
				||invoker.weaponstatus[FALG_CHAMBER]>0
			)setweaponstate("reloadend");
		}
	
	/*loadmag_box:
		FAL2 I 18 offset(2,34) A_StartSound("weapons/pocket",CHAN_WEAPON);
		FAL2 I 4 offset(4,34) A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
		FAL2 J 4 offset(3,38) A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
		FAL2 K 4 offset(2,34) A_StartSound("weapons/FALmagin",CHAN_BODY);
		FAL2 L 5 offset(3,38);
		FAL2 M 2 offset(0,34);
		FAL2 N 2{
			let mmm=hdmagammo(findinventory("HD_FALBoxMag"));
			if(mmm){
				invoker.weaponstatus[FALG_MAG]=mmm.TakeMag(true);
				A_StartSound("weapons/FALmagclick",CHAN_BODY);
			}
		}
		FAL2 O 3;
		#### # 0 A_JumpIf(invoker.weaponstatus[FALG_CHAMBER]<=1,"chamber");
		#### # 0{
			if(
				invoker.weaponstatus[FALG_MAG]<1
				||invoker.weaponstatus[FALG_CHAMBER]>0
			)setweaponstate("reloadend");
		}*/
	//goto chamber;
	
	reloadend:
		---- A 0 A_JumpIf(invoker.weaponstatus[FALG_MAG]<0,"nomag_reloadend");
		FALG PQRS 3;
		goto chamber_manual;
	nomag_reloadend:	
		F5LG IHGEDCBA 2;
		goto nope;

	//altfire:
	//	falg a 1 {invoker.weaponstatus[FALG_MAGTYPE]=1;}
	//	goto nope;

	spawn:
		FALP ABC -1 nodelay{
			if(invoker.weaponstatus[FALG_MAG]<0){frame=1;}
			else if((invoker.weaponstatus[FALG_MAG]>-1)&&(invoker.weaponstatus[FALG_MAGTYPE]==1)){frame=2;}
		}
	}
	override void initializewepstats(bool idfa){
		weaponspecial = 1337;
		//weaponstatus[0]=0;
		weaponstatus[FALG_MAG]=75;
		weaponstatus[FALG_CHAMBER]=2;
		weaponstatus[FALG_MAGTYPE]=0;
		if(!idfa)weaponstatus[FALG_AUTO]=0;
	}
	
	override void loadoutconfigure(string input){

		int bolt=getloadoutvar(input,"bolt",1);
		if(!bolt)weaponstatus[0]&=~FALF_HEAVYBOLT;
		else if(bolt>0)weaponstatus[0]|=FALF_HEAVYBOLT;
	}
}
enum FALstatus{
	FALF_JUSTUNLOAD=1,
	FALF_HEAVYBOLT=8, 
	FALF_RECAST=16,

	FALG_FLAGS=0,
	FALG_MAG=1, //-1 unmagged
	FALG_CHAMBER=2, //0 empty, 1 spent, 2 loaded
	FALG_AUTO=3, //0 semi, 1 burst, 2 auto
	FALG_MAGTYPE=4,  //0 drum, 1 box mag
	FALG_RECASTS=5,
}
